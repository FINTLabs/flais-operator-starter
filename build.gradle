import org.springframework.boot.gradle.plugin.SpringBootPlugin
import io.franzbecker.gradle.lombok.task.DelombokTask

plugins {
    id 'org.springframework.boot' version '2.7.5' apply false
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id 'java'
    id 'groovy'
    id 'maven-publish'
    id 'java-library'
    id "io.franzbecker.gradle-lombok" version "5.0.0"
}

group = 'no.fintlabs.no'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}


repositories {
    mavenLocal()
    maven {
        url "https://repo.fintlabs.no/releases"
    }
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom SpringBootPlugin.BOM_COORDINATES
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'

    api "io.javaoperatorsdk:operator-framework-spring-boot-starter:3.2.0"

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    //testImplementation 'io.fabric8:crd-generator-apt:6.2.0'
    testImplementation 'cglib:cglib-nodep:3.3.0'
    testImplementation 'org.spockframework:spock-spring:2.3-groovy-3.0'
    testImplementation 'org.spockframework:spock-core:2.3-groovy-3.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

lombok { // optional: values below are the defaults
    version = "1.18.24"
}

task delombok(type: DelombokTask, dependsOn: compileJava) {
    ext.outputDir = file("$buildDir/delombok")
    outputs.dir(outputDir)
    sourceSets.main.java.srcDirs.each {
        inputs.dir(it)
        args(it, "-d", outputDir)
    }
    doFirst {
        outputDir.deleteDir()
    }
}

javadoc {
    dependsOn delombok
    source = delombok.outputDir
    failOnError = false
    title("FLAIS Operator Starter")
}

java {
    withJavadocJar()
    withSourcesJar()
}

tasks.named('test') {
    useJUnitPlatform()
}

apply from: 'https://raw.githubusercontent.com/FINTLabs/fint-buildscripts/master/reposilite.ga.gradle'

